cmake_minimum_required(VERSION 3.0.0)
project(oRatio VERSION 2.2.0)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(JNI)

include(CTest)
enable_testing()

file(GLOB SOURCES main.cpp smt/*.cpp smt/lra/*.cpp smt/ov/*.cpp smt/dl/*.cpp riddle/*.cpp core/*.cpp solver/*.cpp solver/types/*.cpp)
include_directories(smt smt/parallel smt/lra smt/ov smt/dl riddle core solver solver/types api/java/src/main/cpp)

set(TEMPORAL_NETWORK_TYPES DL LA)
set(TEMPORAL_NETWORK_TYPE LA CACHE STRING "Temporal network type")
set_property(CACHE TEMPORAL_NETWORK_TYPE PROPERTY STRINGS ${TEMPORAL_NETWORK_TYPES})

set(HEURISTIC_TYPES h_max h_add)
set(HEURISTIC_TYPE "h_max" CACHE STRING "Heuristic type")
set_property(CACHE HEURISTIC_TYPE PROPERTY STRINGS "h_max" "h_add")

set(HOST "127.0.0.1" CACHE STRING "Socket listener host")
set(PORT 1100 CACHE STRING "Socket listener port")
add_definitions(-DHOST="${HOST}" -DPORT=${PORT})

option(DEFERRABLE_FLAWS "Check for deferrable flaws" ON)
option(GRAPH_PRUNING "Prunes the causal graph before starting the search" ON)
option(CHECK_INCONSISTENCIES "Check inconsistencies at each step" OFF)
option(OPTIMIZE_FOR_NATIVE_ARCH "Optimize for native arch" OFF)
option(BUILD_GUI "Builds the GUI" OFF)
option(BUILD_JAVA_LIB "Builds the Java lib" OFF)

message(STATUS "Temporal network type:    ${TEMPORAL_NETWORK_TYPE}")
if(TEMPORAL_NETWORK_TYPE MATCHES DL)
    add_definitions(-DDL_TN)
    configure_file(solver/init_dl.rddl ${CMAKE_BINARY_DIR}/init.rddl COPYONLY)
elseif(TEMPORAL_NETWORK_TYPE MATCHES LA)
    add_definitions(-DLA_TN)
    configure_file(solver/init_la.rddl ${CMAKE_BINARY_DIR}/init.rddl COPYONLY)
else()
    message(FATAL_ERROR "TEMPORAL_NETWORK_TYPE must be one of ${TEMPORAL_NETWORK_TYPES}")
endif()

message(STATUS "Heuristic type:           ${HEURISTIC_TYPE}")
if(HEURISTIC_TYPE MATCHES h_max)
    add_definitions(-DH_MAX)
elseif(HEURISTIC_TYPE MATCHES h_add)
    add_definitions(-DH_ADD)
else()
    message(FATAL_ERROR "HEURISTIC_TYPE must be one of ${HEURISTIC_TYPES}")
endif()

message(STATUS "Deferrable flaws:         ${DEFERRABLE_FLAWS}")
if(DEFERRABLE_FLAWS)
    add_definitions(-DDEFERRABLE_FLAWS)
endif()

message(STATUS "Graph pruning:            ${GRAPH_PRUNING}")
if(GRAPH_PRUNING)
    add_definitions(-DGRAPH_PRUNING)
endif()

message(STATUS "Check inconsistencies:    ${CHECK_INCONSISTENCIES}")
if(CHECK_INCONSISTENCIES)
    add_definitions(-DCHECK_INCONSISTENCIES)
endif()

message(STATUS "Optimize for native arch: ${OPTIMIZE_FOR_NATIVE_ARCH}")
if(OPTIMIZE_FOR_NATIVE_ARCH)
    if(MSVC)
        message(STATUS "Using AVX2..")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -arch:AVX2")
    else()
        message(STATUS "Using native optimization..")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -march=native")
    endif()
endif()

message(STATUS "Build GUI:                ${BUILD_GUI}")
if(BUILD_GUI)
    message(STATUS "Socket listener host:     ${HOST}")
    message(STATUS "Socket listener port:     ${PORT}")
    add_definitions(-DBUILD_GUI)
    list(APPEND SOURCES api/java/src/main/cpp/socket_listener.cpp)
endif()

add_executable(${PROJECT_NAME} ${SOURCES})

if(BUILD_GUI AND WIN32)
    target_link_libraries(${PROJECT_NAME} PRIVATE wsock32 ws2_32 #[[-PROFILE]])
endif()

message(STATUS "Build Java bindings:      ${BUILD_JAVA_LIB}")
if(BUILD_JAVA_LIB)
    add_definitions(-DBUILD_JAVA_LIB)
    include_directories(${JNI_INCLUDE_DIRS})
    list(APPEND SOURCES api/java/src/main/cpp/it_cnr_istc_pst_oratio_Solver.cpp)
    add_library ("${PROJECT_NAME}-lib" SHARED ${SOURCES})
    if(BUILD_GUI AND WIN32)
        target_link_libraries("${PROJECT_NAME}-lib" PRIVATE wsock32 ws2_32 #[[-PROFILE]])
    endif()
endif()

add_subdirectory(smt/tests)
add_subdirectory(riddle/tests)
add_subdirectory(core/tests)
add_subdirectory(solver/tests)

set(CPACK_PROJECT_NAME ${PROJECT_NAME})
set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
include(CPack)
