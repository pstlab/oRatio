class Location {

    real id;
    real city;

    Location(real id, real city) : id(id), city(city) {}
}

class Airport : Location {

    Airport(real id, real city) : Location(id, city) {}
}

class Truck : StateVariable {

    real id;

    Truck(real id) : id(id) {}

    predicate At(Location l) { goal dt = new DriveTo(l:l, end:start); }
    
    predicate DriveTo(Location l) { goal at = new At(end:start); at.l.city == l.city; }
}

class Airplane : StateVariable {

    real id;

    Airplane(real id) : id(id) {}

    predicate At(Airport a) { goal dt = new FlyTo(a:a, end:start); }
    
    predicate FlyTo(Airport a) { goal at = new At(end:start); }
}

class Package : StateVariable {

    real id;

    Package(real id) : id(id) {}

    predicate At(Location l) { { goal ul = new UnloadTruck(l:l, end:start); } or { goal ul = new UnloadAirplane(a:l, end:start); } }

    predicate UnloadTruck(Truck t, Location l) { 
        goal at_t = new t.At(l:l); at_t.start <= start; at_t.end >= end;
        goal i = new IntoTruck(t:t, end:start);
    }
    
    predicate IntoTruck(Truck t) { goal lo = new LoadTruck(t:t, end:start); }
    
    predicate LoadTruck(Truck t, Location l) {
        goal at_t = new t.At(l:l); at_t.start <= start; at_t.end >= end;
        goal at = new At(l:l, end:start);
    }

    predicate UnloadAirplane(Airplane pn, Airport a) { 
        goal at_pn = new pn.At(a:a); at_pn.start <= start; at_pn.end >= end;
        goal i = new IntoAirplane(pn:pn, end:start);
    }
    
    predicate IntoAirplane(Airplane pn) { goal lo = new LoadAirplane(pn:pn, end:start); }
    
    predicate LoadAirplane(Airplane pn, Airport a) {
        goal at_pn = new pn.At(a:a); at_pn.start <= start; at_pn.end >= end;
        goal at = new At(l:a, end:start);
    }
}